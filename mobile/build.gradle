apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'


android {

    aaptOptions {
        // If jpg/png images are already optimised by an external tool (e.g. ImageAlpha/pngquant)
        // ... disabling the cruncher is actually a great id to reduce build time and ensure
        // ... images optimisation
        cruncherEnabled = false
    }

    dataBinding {
        enabled = true
    }
    publishNonDefault true
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId rootProject.PACKAGE_NAME
        minSdkVersion 14
        targetSdkVersion 23
        versionCode rootProject.VERSION_CODE_MOBILE
        versionName rootProject.VERSION_NAME_MOBILE
        // Test intrumentation runner
        testInstrumentationRunner "com.cremy.sharedtest.runner.RxAndroidJUnitRunner"
    }
    buildTypes {

        //region RELEASE
        release {
            multiDexEnabled false
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG", "false"
        }
        //endregion

        //region DEV
        debug {
            versionNameSuffix = rootProject.PACKAGE_NAME_DEV_SUFFIX

            multiDexEnabled false
            shrinkResources false
            minifyEnabled false // replace runProguard true/false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG", "true"
        }
        //endregion
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'

        // Needed by Dagger/Espresso couple
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    wearApp project(':wear')

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':Shared')

    // Apt dependencies
    apt "com.google.dagger:dagger-compiler:$rootProject.DAGGER_VERSION"
    apt "com.jakewharton:butterknife-compiler:$rootProject.BUTTERKNIFE_VERSION"
    apt "com.android.databinding:compiler:$rootProject.ANDROID_DATA_BINDING_VERSION"

    provided "org.glassfish:javax.annotation:$rootProject.GLASSFISH_JAVAX_ANNOTATION" //Required by Dagger2

    // Testing
    androidTestCompile project(path: ':SharedTest')
}

// Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'
