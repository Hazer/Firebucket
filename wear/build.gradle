apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {

    aaptOptions {
        // If jpg/png images are already optimised by an external tool (e.g. ImageAlpha/pngquant)
        // ... disabling the cruncher is actually a great id to reduce build time and ensure
        // ... images optimisation
        cruncherEnabled = false
    }

    dataBinding {
        enabled = true
    }

    publishNonDefault true
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId rootProject.PACKAGE_NAME
        minSdkVersion 14
        targetSdkVersion 23
        versionCode rootProject.VERSION_CODE_MOBILE
        versionName rootProject.VERSION_NAME_MOBILE
        // Test intrumentation runner
        testInstrumentationRunner "com.cremy.sharedtest.runner.RxAndroidJUnitRunner"
    }
    buildTypes {

        //region RELEASE
        release {
            multiDexEnabled false
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG", "false"
            buildConfigField "String", "API_URL_BASE", rootProject.API_URL_BASE_PROD
        }
        //endregion

        //region DEV
        debug {
            versionNameSuffix = rootProject.PACKAGE_NAME_DEV_SUFFIX

            multiDexEnabled false
            shrinkResources false
            minifyEnabled false // replace runProguard true/false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG", "true"
            buildConfigField "String", "API_URL_BASE", rootProject.API_URL_BASE_DEV
        }
        //endregion
    }


    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'

        // Needed by Dagger/Espresso couple
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    //Release
    final DAGGER_VERSION = rootProject.DAGGER_VERSION
    final BUTTERKNIFE_VERSION = rootProject.BUTTERKNIFE_VERSION
    final GLASSFISH_JAVAX_ANNOTATION = rootProject.GLASSFISH_JAVAX_ANNOTATION
    final WEAR_SUPPORT_VERSION = rootProject.WEAR_SUPPORT_VERSION
    final WEAR_PLAY_SERVICES = rootProject.WEAR_PLAY_SERVICES

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':Shared')

    //Wear
    compile "com.google.android.support:wearable:$WEAR_SUPPORT_VERSION"
    compile "com.google.android.gms:play-services-wearable:$WEAR_PLAY_SERVICES"

    // Apt dependencies
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"
    apt 'com.android.databinding:compiler:1.0-rc1'

    provided "org.glassfish:javax.annotation:$GLASSFISH_JAVAX_ANNOTATION" //Required by Dagger2

    // Testing
    androidTestCompile project(path: ':SharedTest')
}
