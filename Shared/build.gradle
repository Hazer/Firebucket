apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'

android {

    aaptOptions {
        // If jpg/png images are already optimised by an external tool (e.g. ImageAlpha/pngquant)
        // ... disabling the cruncher is actually a great id to reduce build time and ensure
        // ... images optimisation
        cruncherEnabled = false
    }

    dataBinding {
        enabled = true
    }

    defaultPublishConfig "debug"
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1000
        versionName "1.00"
    }
    buildTypes {
        //region RELEASE
        release {
            multiDexEnabled false
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG", "false"
            buildConfigField "String", "API_URL_BASE", rootProject.API_URL_BASE_PROD
        }
        //endregion

        //region DEV
        debug {
            versionNameSuffix = rootProject.PACKAGE_NAME_DEV_SUFFIX

            multiDexEnabled false
            shrinkResources false
            minifyEnabled false // replace runProguard true/false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG", "true"
            buildConfigField "String", "API_URL_BASE", rootProject.API_URL_BASE_DEV
        }
        //endregion
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'

        // Needed by Dagger/Espresso couple
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    //region Dagger 2
    def daggerCompiler = "com.google.dagger:dagger-compiler:$rootProject.DAGGER_VERSION"
    compile  "com.google.dagger:dagger:$rootProject.DAGGER_VERSION"
    provided "org.glassfish:javax.annotation:$rootProject.GLASSFISH_JAVAX_ANNOTATION" //Required by Dagger2
    apt daggerCompiler
    //endregion

    //region Support libraries
    compile "com.android.support:appcompat-v7:$rootProject.SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v4:$rootProject.SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$rootProject.SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$rootProject.SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$rootProject.SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:palette-v7:$rootProject.SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$rootProject.SUPPORT_LIBRARY_VERSION"
    //endregion

    //region Retrofit 2
    compile "com.squareup.retrofit2:retrofit:$rootProject.RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.RETROFIT_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.OKHTTP_VERSION"
    //endregion

    //region ButterKnife
    compile "com.jakewharton:butterknife:$rootProject.BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$rootProject.BUTTERKNIFE_VERSION"
    //endregion

    //region Android Data Binding
    apt "com.android.databinding:compiler:$rootProject.ANDROID_DATA_BINDING_VERSION"
    //endregion

    //region rxJava and rxAndroid
    compile "io.reactivex:rxandroid:$rootProject.RXANDROID_VERSION"
    compile "io.reactivex:rxjava:$rootProject.RXJAVA_VERSION"
    //endregion

    //region GreenRobotUtils
    // https://github.com/remychantenay/GreenRobotUtils
    compile "com.github.remychantenay:greenrobotutils:$rootProject.GREEN_ROBOT_UTILS_VERSION"
    //endregion

    //region Glide
    compile "com.github.bumptech.glide:glide:$rootProject.GLIDE_VERSION"
    //endregion

    //region Firebase
    compile "com.google.firebase:firebase-core:$rootProject.PLAY_SERVICES_VERSION"
    compile "com.google.firebase:firebase-database:$rootProject.PLAY_SERVICES_VERSION"
    compile "com.google.firebase:firebase-auth:$rootProject.PLAY_SERVICES_VERSION"
    compile "com.google.firebase:firebase-crash:$rootProject.PLAY_SERVICES_VERSION"
    //endregion

    testApt daggerCompiler
    //endregion

    // Testing
    def jUnit = "junit:junit:$rootProject.JUNIT_VERSION"
    def mockito = "org.mockito:mockito-core:$rootProject.MOCKITO_VERSION"

    compile jUnit
    compile mockito
}